/*全局配置信息及第三方依赖统一管理*/
project.ext {
    //编译版本
    compileSdkVersion = 27
    //构建工具版本
    buildToolsVersion = '27.0.2'
    //最低版本要求
    minSdkVersion = 23
    //目标版本
    targetSdkVersion = 27
    //版本号
    versionCode = 1
    //版本名称
    versionName = '1.0.1'
    //appID 包名
    applicationId = "com.example.hasee"
    //v包
    androidSupportSdkVersion = '27.0.1'

    isDebug = false;
    isLib = false;
    isSettingDebug = false;
    isNewsDebug = true;

    //设置App配置
    setAppDefaultConfig = {
        extension->
            extension.apply plugin: 'com.android.application'
            extension.apply plugin: 'app-joint'
            extension.description "app"
            setAndroidConfig extension.android
            setDependencies extension.dependencies
    }

    //设置Lib配置
    setLibDefaultConfig = {
        extension ->
            extension.apply plugin: 'com.android.library'
            extension.description "lib"
            setAndroidConfig extension.android
            extension.android {
                defaultConfig {
                    defaultPublishConfig 'release'
                    publishNonDefault true
                }
            }
            setDependencies extension.dependencies
    }

    setDefaultConfig = {
        extension->
            setAndroidConfig extension.android
            setDependencies extension.dependencies
    }

    //设置Android配置
    setAndroidConfig ={
        extension->
            extension.compileSdkVersion 27
            extension.buildToolsVersion "27.0.1"
            extension.defaultConfig {
                targetSdkVersion 27

                testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
                javaCompileOptions {
                    annotationProcessorOptions {
                        arguments = [ moduleName : extension.project.getName() ]
                    }
                }
            }
            extension.buildTypes{
                debug{
                    extension.defaultConfig{minSdkVersion 23}
                }
                release{
                    extension.defaultConfig{minSdkVersion 23}
                }
            }

            extension.dataBinding{
                enabled = true
            }
            extension.project.tasks.whenTaskAdded { task ->
                if (task.name.contains("lint")//不启动lint代码扫描潜在bug可以使用
                        ||task.name.equals("clean")
                        ||task.name.contains("Aidl") //项目中有用到aidl则不可以舍弃这个任务
                        ||task.name.contains("mockableAndroidJar")//用不到测试的时候就可以先关闭
                        ||task.name.contains("UnitTest")//用不到测试的时候就可以先关闭
                        ||task.name.contains("AndroidTest")//用不到NDK和JNI的也关闭掉
                        || task.name.contains("Ndk")
                        || task.name.contains("Jni")
                ) {
                    task.enabled = false
                }
            }
    }

    //设置依赖
    setDependencies = {
        extension->
            extension.compile fileTree(dir: 'libs', include: ['*.jar'])
            extension.annotationProcessor 'com.alibaba:arouter-compiler:1.1.4'
    }

}